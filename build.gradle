plugins {
    id 'maven-publish'
    id 'fabric-loom' version '1.10-SNAPSHOT'

    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id 'com.replaymod.preprocess' version '9d21b334a7'

    // https://github.com/Fallen-Breath/yamlang
    id 'me.fallenbreath.yamlang' version '1.4.1'
}

repositories {
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://maven.terraformersmc.com/releases/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://api.modrinth.com/maven' }
    maven { url 'https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1' }
    mavenCentral()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    api "net.fabricmc:fabric-loader:${project.loader_version}"


	// Uncomment the following line to enable the deprecated Fabric API modules. 
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

    // modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
    modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"
    modImplementation "maven.modrinth:wynntils:v3.3.0-fabric"
    modCompileOnly "maven.modrinth:wynntils:v3.3.0-fabric"
    modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:1.2.1"

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation project(':SequoiaHTTPClient')
    implementation project(path: ':SequoiaTextUtils', configuration: 'namedElements')
    runtimeOnly project(path: ':SequoiaTextUtils', configuration: 'namedElements')

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation project(path: ':SequoiaTextUtils', configuration: 'namedElements')
}


def devAuthDir = rootProject.file('.devauth').absolutePath

loom {
    mixin {
        defaultRefmapName = "hspcolor.refmap.json"
    }

    runConfigs.all {
        property "devauth.configDir", devAuthDir
    }
}

preprocess {
    strictExtraMappings = false

    def mc12104 = createNode('1.21.4', 1_21_04, '')
}

tasks.register('buildAndGather') {
    subprojects {
        dependsOn project.tasks.named('build').get()
    }
    doFirst {
        println 'Gathering builds'
        def buildLibs = { p -> p.buildDir.toPath().resolve('libs')
        }
        delete fileTree(buildLibs(rootProject)) {
            include '*'
        }
        subprojects {
            copy {
                from(buildLibs(project)) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar'
                }
                into buildLibs(rootProject).resolve('v' + project.mod_version)
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
            delete(buildLibs(project))
        }
    }
}

tasks.named('test', Test) {
    useJUnitPlatform()
}
