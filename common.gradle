plugins {
    id 'maven-publish'
    id 'fabric-loom'
    id 'com.replaymod.preprocess'
    id 'me.fallenbreath.yamlang'
}

int mcVersion = project.mcVersion

repositories {
    maven { url = 'https://jitpack.io' }
    maven { url = "https://maven.shedaniel.me/" } // Cloth Config
    maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu
}

// https://github.com/FabricMC/fabric-loader/issues/783
configurations {
    modApi {
        exclude group: 'net.fabricmc', module: 'fabric-loader'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"

    // https://github.com/sshtools/two-slices
    modImplementation 'com.sshtools:two-slices:0.9.4'
    include 'com.sshtools:two-slices:0.9.4'

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.4.0")))
}

loom {
    runConfigs.all {
        // to make sure it generates all "Minecraft Client (:subproject_name)" applications
        ideConfigGenerated = true
        runDir '../../run'
        vmArgs '-Dmixin.debug.export=true'
    }
}

String FABRIC_API_MOD_ID = mcVersion >= 1_18_00 ? 'fabric-api' : 'fabric'
String CLOTH_CONFIG_MOD_ID = mcVersion >= 1_18_00 ? 'cloth-config' : 'cloth-config2'

JavaVersion JAVA_COMPATIBILITY =
        mcVersion >= 1_20_05 ? JavaVersion.VERSION_21 :
                mcVersion >= 1_18_00 ? JavaVersion.VERSION_17 :
                        mcVersion >= 1_17_00 ? JavaVersion.VERSION_16 :
                                JavaVersion.VERSION_1_8

java {
    // These define the java compatibility of the artifacts
    sourceCompatibility = JAVA_COMPATIBILITY
    targetCompatibility = JAVA_COMPATIBILITY
}

String versionSuffix = ''
String fullModVersion = project.mod_version + versionSuffix

version = 'v' + fullModVersion
group = project.maven_group
base.archivesName = project.archives_base_name + '-' + project.minecraft_version

processResources {
    inputs.property "id", project.mod_id
    inputs.property "version", fullModVersion
    inputs.property "minecraft_dependency", project.minecraft_dependency

    filesMatching("fabric.mod.json") {
        expand([
                "id": project.mod_id,
                "version": fullModVersion,
                "minecraft_dependency": project.minecraft_dependency
        ])
    }
    filesMatching('hspcolor.mixins.json') {
        filter { s -> s.replace('{{COMPATIBILITY_LEVEL}}', "JAVA_${JAVA_COMPATIBILITY.ordinal() + 1}") }
    }
    filesMatching('fabric.mod.json') {
        filter { s -> s.replace('{{FABRIC_API}}', FABRIC_API_MOD_ID) }
        filter { s -> s.replace('{{CLOTH_CONFIG}}', CLOTH_CONFIG_MOD_ID) }
    }
}

yamlang {
    targetSourceSets = [sourceSets.main]
    inputDir = 'assets/hspcolor/lang'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

jar {
    from(rootProject.file('LICENSE')) {
        rename { "${it}_${project.archives_base_name}" }
    }
}
